(ns metabase-enterprise.metabot-v3.tools.deftool
  "Macro for defining metabot tool endpoints with standard encoding/decoding behavior."
  (:require
   [malli.core :as mc]
   [malli.transform :as mtx]
   [metabase-enterprise.metabot-v3.context :as metabot-v3.context]
   [metabase.api.macros :as api.macros]
   [metabase.util.malli.registry :as mr]
   [metabase.util.malli.schema :as ms]))

;;; ---------------------------------------------------- Schemas ----------------------------------------------------

(mr/def ::tool-request
  "Base schema for all tool requests. Contains the optional conversation ID and profile ID.
   conversation_id is required for internal metabot calls (for logging/history) but optional
   for external Agent API calls."
  [:map
   [:conversation_id {:optional true} [:maybe ms/UUIDString]]
   [:profile_id {:optional true} [:maybe :string]]])

(def ^:private request-transformer
  "Transformer for encoding tool request arguments (snake_case -> kebab-case)."
  (mtx/transformer {:name :tool-api-request}))

(def ^:private response-transformer
  "Transformer for decoding tool response data (kebab-case -> snake_case)."
  (mtx/transformer {:name :tool-api-response}))

(defn invoke-tool
  "Core tool invocation logic. Handles encoding, calling the handler, decoding, and logging.
  This is the runtime implementation called by endpoints generated by `deftool`.

  Arguments:
  - `body` - The request body containing `:arguments` and `:conversation_id`
  - `request` - The Ring request map (used to extract `:metabot-v3/metabot-id`)
  - `opts` - A map with:
    - `:api-name` - Keyword name for logging (e.g., `:field-values`)
    - `:args-schema` - Malli schema for encoding arguments (optional)
    - `:result-schema` - Malli schema for decoding results (optional)
    - `:handler` - Function to call with encoded arguments (receives args map with :metabot-id)

  The handler receives a single map argument containing the encoded args plus `:metabot-id`
  and `:profile-id` (extracted from the request body)."
  [{:keys [arguments conversation_id profile_id] :as body}
   request
   {:keys [api-name args-schema result-schema handler]}]
  (metabot-v3.context/log (assoc body :api api-name) :llm.log/llm->be)
  (let [metabot-id   (:metabot-v3/metabot-id request)
        encoded-args (cond-> (if args-schema
                               (mc/encode args-schema arguments request-transformer)
                               {})
                       metabot-id (assoc :metabot-id metabot-id)
                       profile_id (assoc :profile-id profile_id))
        raw-result   (handler encoded-args)
        result       (if result-schema
                       (mc/decode result-schema raw-result response-transformer)
                       raw-result)]
    (doto (cond-> result
            conversation_id (assoc :conversation_id conversation_id))
      (metabot-v3.context/log :llm.log/be->llm))))

(defmacro deftool
  "Define a tool endpoint with standard encoding/decoding and logging behavior.

  This macro generates a `defendpoint` with the boilerplate needed for tool endpoints:
  - Logging incoming requests and outgoing responses
  - Encoding arguments from snake_case to kebab-case (via `:tool-api-request` transformer)
  - Decoding results from kebab-case to snake_case (via `:tool-api-response` transformer)
  - Attaching `conversation_id` to responses
  - Extracting `:metabot-id` and `:profile-id` from the request body and including them in the args map

  Options:
    :args-schema    - Malli schema for arguments (optional - omit for no-args tools)
    :args-optional? - If true, arguments are marked as optional in the request body schema
    :result-schema  - Malli schema for result (required for response schema generation)
    :handler        - Function to call. Always receives an args map (may be empty for no-args tools)
                      with `:metabot-id` included when available from the request.
    :version        - API version number (e.g., 1). When present, the version is prepended to the
                      route path (e.g., \"/search\" becomes \"/v1/search\"). This is used for
                      external-facing versioned APIs like the Agent API.

  Example:
    (deftool \"/field-values\"
      \"Return statistics and/or values for a given field.\"
      {:args-schema   ::field-values-arguments
       :result-schema ::field-values-result
       :handler       metabot-v3.tools.field-stats/field-values})

  Versioned example:
    (deftool \"/search\"
      \"Search for entities.\"
      {:version       1
       :args-schema   ::search-arguments
       :result-schema ::search-result
       :handler       search-v1})"
  [route docstring {:keys [args-schema args-optional? result-schema handler version]}]
  (let [api-name        (keyword (subs route 1)) ; "/field-values" -> :field-values
        versioned-route (if version
                          (str "/v" version route)
                          route)
        body-schema     (if args-schema
                          (if args-optional?
                            `[:merge [:map [:arguments {:optional true} ~args-schema]] ::tool-request]
                            `[:merge [:map [:arguments ~args-schema]] ::tool-request])
                          `::tool-request)
        response-schema `[:merge ~result-schema ::tool-request]
        tool-opts       {:api-name      api-name
                         :args-schema   args-schema
                         :result-schema result-schema
                         :handler       handler}]
    `(api.macros/defendpoint :post ~versioned-route :- ~response-schema
       ~docstring
       [~'_route-params ~'_query-params ~'body :- ~body-schema ~'request]
       (invoke-tool ~'body ~'request ~tool-opts))))
