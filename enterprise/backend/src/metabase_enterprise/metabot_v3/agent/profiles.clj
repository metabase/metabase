(ns metabase-enterprise.metabot-v3.agent.profiles
  "Profile configurations for different metabot use cases.
  Each profile defines model settings, iteration limits, and available tools."
  (:require
   [clojure.string :as str]
   [malli.error :as me]
   [metabase-enterprise.metabot-v3.agent.tools :as agent-tools]
   [metabase.util.malli :as mu]
   [metabase.util.malli.registry :as mr]))

(declare validate-tool-definition!)

(def ^:private *profiles
  "Map of profile-id to profile configuration"
  (atom {}))

(mu/defn ^:private register-profile!
  "Register new profile configuration.

  Each profile includes:
  - :prompt-template - Selmer template name from resources/metabot/prompts/system/
  - :model - LLM model identifier
  - :max-iterations - Maximum agent loop iterations
  - :temperature - LLM temperature setting
  - :tools - Vector of tool vars available to this profile"
  [name profile :- [:map
                    [:prompt-template :string]
                    [:model :string]
                    [:max-iterations :int]
                    [:temperature :float]
                    [:tools [:vector :any]]]]
  (when-not (apply distinct? (map #(:tool-name (meta %)) (:tools profile)))
    (let [dups (->> (frequencies (map #(:tool-name (meta %)) (:tools profile)))
                    (filter (fn [[_ cnt]] (< 1 cnt))))]
      (throw (ex-info "Duplicate tool names in profile" {:tool-names (map first dups)}))))
  (doseq [tool (:tools profile)]
    (validate-tool-definition! tool))
  (swap! *profiles assoc name profile))

(register-profile!
 :embedding_next
 {:prompt-template "embedding-next.selmer"
  :model           "claude-haiku-4-5"
  :max-iterations  10
  :temperature     0.3
  :tools           [#'agent-tools/construct-notebook-query-tool
                    #'agent-tools/read-resource-tool
                    #'agent-tools/list-available-data-sources-tool]})

(register-profile!
 :internal
 {:prompt-template "internal.selmer"
  :model           "claude-haiku-4-5"
  :max-iterations  10
  :temperature     0.3
  :tools           [#'agent-tools/search-tool
                    #'agent-tools/construct-notebook-query-tool
                    #'agent-tools/read-resource-tool
                    #'agent-tools/create-sql-query-tool
                    #'agent-tools/edit-sql-query-tool
                    #'agent-tools/replace-sql-query-tool
                    #'agent-tools/edit-chart-tool
                    #'agent-tools/navigate-user-tool
                    #'agent-tools/create-chart-tool
                    #'agent-tools/create-autogenerated-dashboard-tool]})

(register-profile!
 :transforms_codegen
 {:prompt-template "transform-codegen.selmer"
  :model           "claude-haiku-4-5"
  :max-iterations  30
  :temperature     0.3
  :tools           [#'agent-tools/transform-search-tool
                    #'agent-tools/get-transform-details-tool
                    #'agent-tools/get-transform-python-library-details-tool
                    #'agent-tools/write-transform-sql-tool
                    #'agent-tools/write-transform-python-tool
                    #'agent-tools/list-snippets-tool
                    #'agent-tools/get-snippet-details-tool
                    #'agent-tools/list-available-fields-tool
                    #'agent-tools/get-field-values-tool
                    #'agent-tools/todo-write-tool
                    #'agent-tools/todo-read-tool]})

(register-profile!
 :sql
 {:prompt-template     "sql-querying-only.selmer"
  :model               "claude-haiku-4-5"
  :max-iterations      10
  :temperature         0.3
  :required-tool-call? true
  :tools               [#'agent-tools/sql-search-tool
                        #'agent-tools/read-resource-tool
                        #'agent-tools/create-sql-query-code-edit-tool
                        #'agent-tools/edit-sql-query-code-edit-tool
                        #'agent-tools/replace-sql-query-code-edit-tool
                        #'agent-tools/ask-for-sql-clarification-tool]})

(register-profile!
 :nlq
 {:prompt-template "natural-language-querying-only.selmer"
  :model           "claude-haiku-4-5"
  :max-iterations  10
  :temperature     0.3
  :tools           [#'agent-tools/nlq-search-tool
                    #'agent-tools/read-resource-tool
                    #'agent-tools/construct-notebook-query-tool
                    #'agent-tools/navigate-user-tool
                    #'agent-tools/create-chart-tool
                    #'agent-tools/edit-chart-tool]})

(def ^:private tool-definition-schema
  [:map
   [:tool-name [:and :string [:fn #(not (str/blank? %))]]]
   [:schema [:fn some?]]
   [:doc {:optional true} [:maybe :string]]
   [:system-instructions {:optional true} [:maybe :string]]])

(defn- validate-tool-definition!
  [tool-var]
  (let [definition (select-keys (meta tool-var) [:tool-name :schema :doc :system-instructions])]
    (when-not (mr/validate tool-definition-schema definition)
      (throw (ex-info "Invalid tool definition metadata"
                      {:tool       tool-var
                       :definition definition
                       :errors     (me/humanize (mu/explain tool-definition-schema definition))})))))

(defn- filter-by-capabilities
  "Filter tool vars by user capabilities.
  Removes tools that require capabilities the user doesn't have."
  [tool-vars capabilities]
  (let [capabilities-set (set capabilities)]
    (filter (fn [tool-var]
              (every? capabilities-set (:capabilities (meta tool-var))))
            tool-vars)))

(defn- tool-map
  [tools]
  (into {} (map (juxt #(:tool-name (meta %)) identity) tools)))

;;; API

(defn get-profile
  "Get profile configuration by profile-id keyword."
  [profile-id]
  (get @*profiles profile-id))

(defn get-tools-for-profile
  "Get tool registry filtered by profile configuration and user capabilities."
  [profile-id capabilities]
  (some-> (get-profile profile-id)
          :tools
          (filter-by-capabilities capabilities)
          tool-map))

