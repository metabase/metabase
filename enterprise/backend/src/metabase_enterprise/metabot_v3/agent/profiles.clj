(ns metabase-enterprise.metabot-v3.agent.profiles
  "Profile configurations for different metabot use cases.
  Each profile defines model settings, iteration limits, and available tools."
  (:require
   [clojure.string :as str]
   [malli.error :as me]
   [metabase-enterprise.llm.settings :as llm]
   [metabase-enterprise.metabot-v3.agent.tools :as agent-tools]
   [metabase.util.malli :as mu]
   [metabase.util.malli.registry :as mr]))

(def ^:private tool-definition-schema
  [:map
   [:tool-name [:and :string [:fn #(not (str/blank? %))]]]
   [:schema [:fn some?]]
   [:doc {:optional true} [:maybe :string]]
   [:prompt {:optional true} [:maybe :string]]])

(def ^:private *profiles
  "Map of profile-id to profile configuration"
  (atom {}))

(defn- validate-tool-definition!
  [tool-var]
  (let [definition (select-keys (meta tool-var) [:tool-name :schema :doc :prompt])]
    (when-not (mr/validate tool-definition-schema definition)
      (throw (ex-info "Invalid tool definition metadata"
                      {:tool       tool-var
                       :definition definition
                       :errors     (me/humanize (mu/explain tool-definition-schema definition))})))))

(mu/defn ^:private register-profile!
  "Register new profile configuration.

  Each profile includes:
  - :name - Keyword identifier for the profile (e.g. :internal)
  - :prompt-template - Selmer template name from resources/metabot/prompts/system/
  - :max-iterations - Maximum agent loop iterations
  - :temperature - LLM temperature setting
  - :tools - Vector of tool vars available to this profile

  Note: `:model` is resolved at runtime from the `ee-ai-metabot-provider` setting
  (see [[get-profile]]), not stored in the profile."
  [profile :- [:map
               [:name :keyword]
               [:prompt-template :string]
               [:max-iterations :int]
               [:temperature :float]
               [:tools [:vector :any]]]]
  (when-not (apply distinct? (map #(:tool-name (meta %)) (:tools profile)))
    (let [dups (->> (frequencies (map #(:tool-name (meta %)) (:tools profile)))
                    (filter (fn [[_ cnt]] (< 1 cnt))))]
      (throw (ex-info "Duplicate tool names in profile" {:tool-names (map first dups)}))))
  (doseq [tool (:tools profile)]
    (validate-tool-definition! tool))
  (swap! *profiles assoc (:name profile) profile))

(register-profile!
 {:name            :embedding_next
  :prompt-template "embedding-next.selmer"
  :max-iterations  10
  :temperature     0.3
  :tools           [#'agent-tools/construct-notebook-query-tool
                    #'agent-tools/read-resource-tool
                    #'agent-tools/list-available-data-sources-tool]})

(register-profile!
 {:name            :internal
  :prompt-template "internal.selmer"
  :max-iterations  10
  :temperature     0.3
  :tools           [#'agent-tools/search-tool
                    #'agent-tools/construct-notebook-query-tool
                    #'agent-tools/read-resource-tool
                    #'agent-tools/create-sql-query-tool
                    #'agent-tools/edit-sql-query-tool
                    #'agent-tools/replace-sql-query-tool
                    #'agent-tools/edit-chart-tool
                    #'agent-tools/navigate-user-tool
                    #'agent-tools/create-chart-tool
                    #'agent-tools/create-autogenerated-dashboard-tool
                    #'agent-tools/create-dashboard-subscription-tool
                    #'agent-tools/find-outliers-tool]})

(register-profile!
 {:name            :transforms_codegen
  :prompt-template "transform-codegen.selmer"
  :max-iterations  30
  :temperature     0.3
  :tools           [#'agent-tools/transform-search-tool
                    #'agent-tools/get-transform-details-tool
                    #'agent-tools/get-transform-python-library-details-tool
                    #'agent-tools/write-transform-sql-tool
                    #'agent-tools/write-transform-python-tool
                    #'agent-tools/list-snippets-tool
                    #'agent-tools/get-snippet-details-tool
                    #'agent-tools/list-available-fields-tool
                    #'agent-tools/get-field-values-tool
                    #'agent-tools/todo-write-tool
                    #'agent-tools/todo-read-tool]})

(register-profile!
 {:name                :sql
  :prompt-template     "sql-querying-only.selmer"
  :max-iterations      10
  :temperature         0.3
  :required-tool-call? true
  :tools               [#'agent-tools/sql-search-tool
                        #'agent-tools/read-resource-tool
                        #'agent-tools/create-sql-query-code-edit-tool
                        #'agent-tools/edit-sql-query-code-edit-tool
                        #'agent-tools/replace-sql-query-code-edit-tool
                        #'agent-tools/ask-for-sql-clarification-tool]})

(register-profile!
 {:name            :nlq
  :prompt-template "natural-language-querying-only.selmer"
  :max-iterations  10
  :temperature     0.3
  :tools           [#'agent-tools/nlq-search-tool
                    #'agent-tools/read-resource-tool
                    #'agent-tools/construct-notebook-query-tool
                    #'agent-tools/navigate-user-tool
                    #'agent-tools/create-chart-tool
                    #'agent-tools/edit-chart-tool]})

(register-profile!
 {:name            :document-generate-content
  :prompt-template "document-generate-content.selmer"
  :max-iterations  10
  :temperature     0.3
  :required-tool-call? true
  :tools           [#'agent-tools/list-available-data-sources-tool
                    #'agent-tools/list-available-fields-tool
                    #'agent-tools/get-field-values-tool
                    #'agent-tools/document-schema-collect-tool
                    #'agent-tools/document-construct-model-chart-tool
                    #'agent-tools/document-construct-sql-chart-tool]})

(def ^:private api-string->capability-keyword
  "Map from API capability strings (as sent by the frontend) to the keywords
  used in tool :capabilities metadata. Keywords pass through unchanged."
  {"frontend:navigate_user_v1"    :frontend-navigate-user-v1
   "permission:save_questions"    :permission-save-questions
   "permission:write_sql_queries" :permission-write-sql-queries
   "permission:write_transforms"  :permission-write-transforms
   "feature:transforms"           :feature-transforms
   "feature:transforms-python"    :feature-transforms-python
   "feature:snippets"             :feature-snippets
   "automagic-dashboards"         :automagic-dashboards})

(defn- capability->keyword
  "Normalize a capability value to a keyword. Strings are looked up in the
  api-string->capability-keyword map; keywords are returned as-is."
  [cap]
  (if (keyword? cap)
    cap
    (or (api-string->capability-keyword cap)
        ;; Fallback: keywordize unknown strings so new capabilities degrade
        ;; gracefully without requiring a map update.
        (keyword (-> (str cap) (str/replace ":" "-") (str/replace "_" "-"))))))

(defn- filter-by-capabilities
  "Filter tool vars by user capabilities.
  Removes tools that require capabilities the user doesn't have.
  Capabilities from the API arrive as strings (e.g. \"frontend:navigate_user_v1\")
  while tool metadata uses keywords (e.g. :frontend-navigate-user-v1), so we
  normalize to keywords before comparing."
  [tool-vars capabilities]
  (let [capabilities-set (into #{} (map capability->keyword) capabilities)]
    (filter (fn [tool-var]
              (every? capabilities-set (:capabilities (meta tool-var))))
            tool-vars)))

(defn- tool-map
  [tools]
  (into {} (map (juxt #(:tool-name (meta %)) identity) tools)))

;;; API

(defn get-profile
  "Get profile configuration by profile-id keyword.
  The `:model` in the returned profile is resolved from the `ee-ai-metabot-provider`
  setting at call time, so it always reflects the current admin configuration."
  [profile-id]
  (when-let [profile (get @*profiles profile-id)]
    (assoc profile :model (llm/ee-ai-metabot-provider))))

(defn get-tools-for-profile
  "Get tool registry filtered by profile configuration and user capabilities."
  [profile-id capabilities]
  (some-> (get-profile profile-id)
          :tools
          (filter-by-capabilities capabilities)
          tool-map))
