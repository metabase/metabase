(ns metabase-enterprise.metabot-v3.agent.profiles
  "Profile configurations for different metabot use cases.
  Each profile defines model settings, iteration limits, and available tools."
  (:require
   [clojure.string :as str]
   [malli.core :as m]
   [malli.error :as me]
   [metabase-enterprise.metabot-v3.agent.tools :as agent-tools]))

(def profiles
  "Map of profile-id to profile configuration.

  Each profile includes:
  - :prompt-template - Selmer template name from resources/metabot/prompts/system/
  - :model - LLM model identifier
  - :max-iterations - Maximum agent loop iterations
  - :temperature - LLM temperature setting
  - :tools - Vector of tool vars available to this profile"
  {:embedding_next
   {:prompt-template "embedding-next.selmer"
    :model "claude-haiku-4-5"
    :max-iterations 10
    :temperature 0.3
    :tools [#'agent-tools/construct-notebook-query-tool
            #'agent-tools/read-resource-tool
            #'agent-tools/list-available-data-sources-tool]}

   :internal
   {:prompt-template "internal.selmer"
    :model "claude-haiku-4-5"
    :max-iterations 10
    :temperature 0.3
    :tools [#'agent-tools/search-tool
            #'agent-tools/construct-notebook-query-tool
            #'agent-tools/read-resource-tool
            #'agent-tools/create-sql-query-tool
            #'agent-tools/edit-sql-query-tool
            #'agent-tools/replace-sql-query-tool
            #'agent-tools/edit-chart-tool
            #'agent-tools/navigate-user-tool
            #'agent-tools/create-chart-tool
            #'agent-tools/create-autogenerated-dashboard-tool]}

   :transforms_codegen
   {:prompt-template "transform-codegen.selmer"
    :model "claude-haiku-4-5"
    :max-iterations 30
    :temperature 0.3
    :tools [#'agent-tools/search-tool
            #'agent-tools/get-transform-details-tool
            #'agent-tools/get-transform-python-library-details-tool
            #'agent-tools/write-transform-sql-tool
            #'agent-tools/write-transform-python-tool
            #'agent-tools/list-snippets-tool
            #'agent-tools/get-snippet-details-tool
            #'agent-tools/list-available-fields-tool
            #'agent-tools/get-field-values-tool
            #'agent-tools/todo-write-tool
            #'agent-tools/todo-read-tool]}

   :sql
   {:prompt-template "sql-querying-only.selmer"
    :model "claude-haiku-4-5"
    :max-iterations 10
    :temperature 0.3
    :required-tool-call? true
    :tools [#'agent-tools/search-tool
            #'agent-tools/read-resource-tool
            #'agent-tools/create-sql-query-tool
            #'agent-tools/edit-sql-query-tool
            #'agent-tools/replace-sql-query-tool
            #'agent-tools/ask-for-sql-clarification-tool]}

   :nlq
   {:prompt-template "natural-language-querying-only.selmer"
    :model "claude-haiku-4-5"
    :max-iterations 10
    :temperature 0.3
    :tools [#'agent-tools/search-tool
            #'agent-tools/read-resource-tool
            #'agent-tools/construct-notebook-query-tool
            #'agent-tools/navigate-user-tool
            #'agent-tools/create-chart-tool
            #'agent-tools/edit-chart-tool]}})

(def ^:private profile-tool-overrides
  {:sql {#'agent-tools/search-tool #'agent-tools/sql-search-tool
         #'agent-tools/create-sql-query-tool #'agent-tools/create-sql-query-code-edit-tool
         #'agent-tools/edit-sql-query-tool #'agent-tools/edit-sql-query-code-edit-tool
         #'agent-tools/replace-sql-query-tool #'agent-tools/replace-sql-query-code-edit-tool}
   :nlq {#'agent-tools/search-tool #'agent-tools/nlq-search-tool}
   :transforms_codegen {#'agent-tools/search-tool #'agent-tools/transform-search-tool}})

(def ^:private tool-capabilities
  "Map of tool vars to required capabilities.
  Tools not listed here have no special capability requirements."
  {#'agent-tools/navigate-user-tool #{:frontend-navigate-user-v1}
   #'agent-tools/create-autogenerated-dashboard-tool #{:automagic-dashboards}
   #'agent-tools/write-transform-sql-tool #{:feature-transforms :permission-write-transforms}
   #'agent-tools/write-transform-python-tool #{:feature-transforms :feature-transforms-python :permission-write-transforms}
   #'agent-tools/list-snippets-tool #{:feature-snippets}
   #'agent-tools/get-snippet-details-tool #{:feature-snippets}})

(def ^:private tool-definition-schema
  [:map
   [:tool-name [:and :string [:fn #(not (str/blank? %))]]]
   [:var [:fn var?]]
   [:schema [:fn some?]]
   [:doc {:optional true} [:maybe :string]]
   [:system-instructions {:optional true} [:maybe :string]]])

(defn- tool-definition
  [tool-var]
  (let [{:keys [tool-name schema doc system-instructions]} (meta tool-var)]
    {:tool-name tool-name
     :var tool-var
     :schema schema
     :doc doc
     :system-instructions system-instructions}))

(defn- validate-tool-definition!
  [tool-var]
  (let [definition (tool-definition tool-var)]
    (when-not (m/validate tool-definition-schema definition)
      (throw (ex-info "Invalid tool definition metadata"
                      {:tool-var tool-var
                       :definition definition
                       :errors (me/humanize (m/explain tool-definition-schema definition))})))))

(declare apply-overrides tool-map)

(defn all-tool-vars
  "Return all tool vars referenced by profiles and overrides."
  []
  (->> (concat (mapcat :tools (vals profiles))
               (mapcat vals (vals profile-tool-overrides)))
       distinct
       vec))

(defn validate-tool-definitions!
  "Validate tool definitions at startup."
  []
  (let [tool-vars (all-tool-vars)]
    (doseq [tool-var tool-vars]
      (validate-tool-definition! tool-var))
    (doseq [[profile-id profile] profiles]
      (let [tool-vars (apply-overrides (:tools profile)
                                       (get profile-tool-overrides profile-id {}))]
        (tool-map tool-vars)))
    true))

(defn filter-by-capabilities
  "Filter tool vars by user capabilities.
  Removes tools that require capabilities the user doesn't have."
  [tool-vars capabilities]
  (let [capabilities-set (set capabilities)]
    (filter (fn [tool-var]
              (let [required (get tool-capabilities tool-var #{})]
                (every? capabilities-set required)))
            tool-vars)))

(defn- apply-overrides
  [tool-vars overrides]
  (mapv (fn [tool-var] (get overrides tool-var tool-var)) tool-vars))

(defn- tool-map
  [tool-vars]
  (let [entries (map (fn [tool-var] [(:tool-name (meta tool-var)) tool-var]) tool-vars)
        duplicates (->> entries (map first) frequencies (filter (fn [[_ count]] (> count 1))) seq)]
    (when duplicates
      (throw (ex-info "Duplicate tool names in profile"
                      {:tool-names (map first duplicates)})))
    (into {} entries)))

(defn get-profile
  "Get profile configuration by profile-id keyword."
  [profile-id]
  (get profiles profile-id))

(defn get-tools-for-profile
  "Get tool registry filtered by profile configuration and user capabilities."
  [profile-id capabilities]
  (if-let [profile (get-profile profile-id)]
    (let [tool-vars (:tools profile)
          overrides (get profile-tool-overrides profile-id {})
          tool-vars (apply-overrides tool-vars overrides)
          tool-vars (filter-by-capabilities tool-vars capabilities)]
      (tool-map tool-vars))
    {}))

(validate-tool-definitions!)
