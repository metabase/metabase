(ns metabase-enterprise.metabot-v3.agent.tools
  "Tool registry for the agent loop.
  Provides a unified namespace for tool vars and state-aware wrapping."
  (:require
   [metabase-enterprise.metabot-v3.agent.tools.autogen-dashboard :as autogen-dashboard-tools]
   [metabase-enterprise.metabot-v3.agent.tools.charts :as charts-tools]
   [metabase-enterprise.metabot-v3.agent.tools.clarification :as clarification-tools]
   [metabase-enterprise.metabot-v3.agent.tools.construct :as construct-tools]
   [metabase-enterprise.metabot-v3.agent.tools.metadata :as metadata-tools]
   [metabase-enterprise.metabot-v3.agent.tools.navigation :as navigation-tools]
   [metabase-enterprise.metabot-v3.agent.tools.resources :as resources-tools]
   [metabase-enterprise.metabot-v3.agent.tools.search :as search-tools]
   [metabase-enterprise.metabot-v3.agent.tools.shared :as shared]
   [metabase-enterprise.metabot-v3.agent.tools.snippets :as snippets-tools]
   [metabase-enterprise.metabot-v3.agent.tools.sql :as sql-tools]
   [metabase-enterprise.metabot-v3.agent.tools.todo :as todo-tools]
   [metabase-enterprise.metabot-v3.agent.tools.transforms :as transforms-tools]))

(set! *warn-on-reflection* true)

(defn- alias-tool-var
  [sym tool-var]
  (let [alias-var (intern *ns* sym @tool-var)]
    (alter-meta! alias-var merge (meta tool-var))
    alias-var))

(alias-tool-var 'search-tool #'search-tools/search-tool)
(alias-tool-var 'sql-search-tool #'search-tools/sql-search-tool)
(alias-tool-var 'nlq-search-tool #'search-tools/nlq-search-tool)
(alias-tool-var 'transform-search-tool #'search-tools/transform-search-tool)
(alias-tool-var 'construct-notebook-query-tool #'construct-tools/construct-notebook-query-tool)
(alias-tool-var 'list-available-data-sources-tool #'metadata-tools/list-available-data-sources-tool)
(alias-tool-var 'list-available-fields-tool #'metadata-tools/list-available-fields-tool)
(alias-tool-var 'get-field-values-tool #'metadata-tools/get-field-values-tool)
(alias-tool-var 'get-transform-details-tool #'transforms-tools/get-transform-details-tool)
(alias-tool-var 'get-transform-python-library-details-tool #'transforms-tools/get-transform-python-library-details-tool)
(alias-tool-var 'write-transform-sql-tool #'transforms-tools/write-transform-sql-tool)
(alias-tool-var 'write-transform-python-tool #'transforms-tools/write-transform-python-tool)
(alias-tool-var 'create-sql-query-tool #'sql-tools/create-sql-query-tool)
(alias-tool-var 'create-sql-query-code-edit-tool #'sql-tools/create-sql-query-code-edit-tool)
(alias-tool-var 'edit-sql-query-tool #'sql-tools/edit-sql-query-tool)
(alias-tool-var 'edit-sql-query-code-edit-tool #'sql-tools/edit-sql-query-code-edit-tool)
(alias-tool-var 'replace-sql-query-tool #'sql-tools/replace-sql-query-tool)
(alias-tool-var 'replace-sql-query-code-edit-tool #'sql-tools/replace-sql-query-code-edit-tool)
(alias-tool-var 'create-chart-tool #'charts-tools/create-chart-tool)
(alias-tool-var 'edit-chart-tool #'charts-tools/edit-chart-tool)
(alias-tool-var 'read-resource-tool #'resources-tools/read-resource-tool)
(alias-tool-var 'todo-write-tool #'todo-tools/todo-write-tool)
(alias-tool-var 'todo-read-tool #'todo-tools/todo-read-tool)
(alias-tool-var 'navigate-user-tool #'navigation-tools/navigate-user-tool)
(alias-tool-var 'list-snippets-tool #'snippets-tools/list-snippets-tool)
(alias-tool-var 'get-snippet-details-tool #'snippets-tools/get-snippet-details-tool)
(alias-tool-var 'ask-for-sql-clarification-tool #'clarification-tools/ask-for-sql-clarification-tool)
(alias-tool-var 'create-autogenerated-dashboard-tool #'autogen-dashboard-tools/create-autogenerated-dashboard-tool)

(def ^:dynamic *memory-atom* nil)

(def ^:private state-dependent-tools
  "Set of tool names that require access to agent state."
  #{"create_chart" "edit_chart" "todo_write" "todo_read" "navigate_user"
    "write_transform_sql" "write_transform_python" "create_autogenerated_dashboard"
    "create_sql_query" "edit_sql_query" "replace_sql_query"})

(defn wrap-tools-with-state
  "Wrap state-dependent tools with access to the memory atom.
  Tools in `state-dependent-tools` will have access to the memory atom
  via the dynamic `*memory-atom*` binding.

  Returns a new tools map where state-dependent tools are wrapped.
  Wrapped tools are returned as maps with :doc, :schema, and :fn keys
  to satisfy the Claude API schema validation."
  [tools memory-atom]
  (reduce-kv
   (fn [acc tool-name tool-var]
     (if (contains? state-dependent-tools tool-name)
       (let [{:keys [doc schema system-instructions]} (meta tool-var)
             wrapped-fn (fn [args]
                          (binding [shared/*memory-atom* memory-atom
                                    *memory-atom* memory-atom]
                            (tool-var args)))]
         (assoc acc tool-name {:doc doc
                               :schema schema
                               :system-instructions system-instructions
                               :fn wrapped-fn}))
       (assoc acc tool-name tool-var)))
   {}
   tools))
