(ns metabase-enterprise.metabot-v3.agent.tools
  "Tool registry for the agent loop.
  Provides a unified namespace for tool vars and state-aware wrapping."
  (:require
   [metabase-enterprise.metabot-v3.agent.tools.autogen-dashboard :as tools.autogen-dashboard]
   [metabase-enterprise.metabot-v3.agent.tools.charts :as tools.charts]
   [metabase-enterprise.metabot-v3.agent.tools.clarification :as tools.clarification]
   [metabase-enterprise.metabot-v3.agent.tools.construct :as tools.construct]
   [metabase-enterprise.metabot-v3.agent.tools.metadata :as tools.metadata]
   [metabase-enterprise.metabot-v3.agent.tools.navigation :as tools.navigation]
   [metabase-enterprise.metabot-v3.agent.tools.resources :as tools.resources]
   [metabase-enterprise.metabot-v3.agent.tools.search :as tools.search]
   [metabase-enterprise.metabot-v3.agent.tools.shared :as shared]
   [metabase-enterprise.metabot-v3.agent.tools.snippets :as tools.snippets]
   [metabase-enterprise.metabot-v3.agent.tools.sql :as tools.sql]
   [metabase-enterprise.metabot-v3.agent.tools.todo :as tools.todo]
   [metabase-enterprise.metabot-v3.agent.tools.transforms :as tools.transforms]
   [potemkin :as p]))

(set! *warn-on-reflection* true)

(p/import-vars
 [tools.search search-tool
  sql-search-tool
  nlq-search-tool
  transform-search-tool]
 [tools.construct
  construct-notebook-query-tool]
 [tools.metadata
  list-available-data-sources-tool
  list-available-fields-tool
  get-field-values-tool]
 [tools.transforms
  get-transform-details-tool
  get-transform-python-library-details-tool
  write-transform-sql-tool
  write-transform-python-tool]
 [tools.sql
  create-sql-query-tool
  create-sql-query-code-edit-tool
  edit-sql-query-tool
  edit-sql-query-code-edit-tool
  replace-sql-query-tool
  replace-sql-query-code-edit-tool]
 [tools.charts
  create-chart-tool
  edit-chart-tool]
 [tools.resources
  read-resource-tool]
 [tools.todo
  todo-write-tool
  todo-read-tool]
 [tools.navigation
  navigate-user-tool]
 [tools.snippets
  list-snippets-tool
  get-snippet-details-tool]
 [tools.clarification
  ask-for-sql-clarification-tool]
 [tools.autogen-dashboard
  create-autogenerated-dashboard-tool])

(def ^:dynamic ^:private *memory-atom* nil)

(def ^:private state-dependent-tools
  "Set of tool names that require access to agent state."
  #{"create_chart" "edit_chart" "todo_write" "todo_read" "navigate_user"
    "write_transform_sql" "write_transform_python" "create_autogenerated_dashboard"
    "create_sql_query" "edit_sql_query" "replace_sql_query"})

(defn wrap-tools-with-state
  "Wrap state-dependent tools with access to the memory atom.
  Tools in `state-dependent-tools` will have access to the memory atom
  via the dynamic `*memory-atom*` binding.

  Returns a new tools map where state-dependent tools are wrapped.
  Wrapped tools are returned as maps with :doc, :schema, and :fn keys
  to satisfy the Claude API schema validation."
  [tools memory-atom]
  (reduce-kv
   (fn [acc tool-name tool-var]
     (if (contains? state-dependent-tools tool-name)
       (let [{:keys [doc schema system-instructions]} (meta tool-var)
             wrapped-fn (fn [args]
                          (binding [shared/*memory-atom* memory-atom
                                    *memory-atom* memory-atom]
                            (tool-var args)))]
         (assoc acc tool-name {:doc doc
                               :schema schema
                               :system-instructions system-instructions
                               :fn wrapped-fn}))
       (assoc acc tool-name tool-var)))
   {}
   tools))
